plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
}
boolean enableFTBTeams = true
dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-$minecraft_version:$parchment_version@zip")
    }
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    testCompileOnly 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":common")
    
    modImplementation "me.fzzyhmstrs:fzzy_config:${fzzy_config_version}+${minecraft_version}"
    modImplementation "curse.maven:fabric-language-kotlin-308769:4718458"

    modImplementation ("maven.modrinth:nirvana-library:${project.name}-${minecraft_version}-${lib_version}")
    //modImplementation ("blank:Nirvana Lib-fabric-1.20.1:2.0.8")
    modImplementation("maven.modrinth:common-network:hQsMT8cT"){
        transitive = false
    }

    if (enableFTBTeams){
        modImplementation "curse.maven:ftb-teams-fabric-438497:6130783"
        modImplementation "curse.maven:architectury-api-419699:5137936"
        modImplementation "curse.maven:ftb-library-fabric-438495:6164052"
    } else {
        modCompileOnly "curse.maven:ftb-teams-fabric-438497:6130783"
    }
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}



